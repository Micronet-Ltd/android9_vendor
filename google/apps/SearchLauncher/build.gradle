// The Gradle version is only for development, use 'make' to build to release apks
buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha16'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

final String SUPPORT_LIBS_VERSION = '28.0.0-SNAPSHOT'

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

final String ANDROID_TOP = "${rootDir}/../../../../.."
final String AOSP_DIR = "${ANDROID_TOP}/packages/apps/Launcher3"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.0'

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 28
        versionCode 10000
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        simple {
            dimension "default"
            applicationId 'com.android.launcher3'
        }

        quickstep {
            dimension "default"
            applicationId 'com.android.launcher3'
        }
    }

    // Disable release builds for now
    android.variantFilter { variant ->
        if (variant.buildType.name.endsWith('release')) {
            variant.setIgnore(true);
        }
    }

    sourceSets {
        main {
            java.srcDirs = ["${AOSP_DIR}/src", "${AOSP_DIR}/protos/", "src"]
            res.srcDirs = ["${AOSP_DIR}/res"]
            manifest.srcFile "${ANDROID_TOP}/common/AndroidManifest.xml"
            proto {
                srcDir "${AOSP_DIR}/protos/"
                srcDir "${AOSP_DIR}/proto_overrides/"
            }
        }

        simple {
            java.srcDirs = ["${AOSP_DIR}/src_flags", "${AOSP_DIR}/src_ui_overrides"]
            res.srcDirs = ["res"]
            manifest.srcFile "AndroidManifest.xml"
        }

        quickstep {
            java.srcDirs = ["${AOSP_DIR}/quickstep/src", "src", "quickstep/src"]
            res.srcDirs = ["${AOSP_DIR}/quickstep/res", "res"]
            manifest.srcFile "${AOSP_DIR}/quickstep/AndroidManifest.xml"
        }

        quickstepDebug {
            res.srcDirs = ["quickstep/res"]
            manifest.srcFile "AndroidManifest.xml"
        }
    }
}

repositories {
    maven { url "../../../../../prebuilts/fullsdk-darwin/extras/android/m2repository" }
    maven { url "../../../../../prebuilts/fullsdk-linux/extras/android/m2repository" }
    mavenCentral()
    google()
}

dependencies {
    implementation "com.android.support:support-v4:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-dynamic-animation:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBS_VERSION}"
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'
    implementation fileTree(dir: 'libs', include: '*.jar')

    quickstepImplementation fileTree(dir: "${AOSP_DIR}/quickstep/libs", include: 'sysui_shared.jar')
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_log.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_dump.proto|com.android.launcher3.model.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}
